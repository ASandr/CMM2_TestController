'**************************************************
'* Program fot testing WII Controller Classic Pro *
'* Author: Alexandrov A.S.                        *
'* Date: 10 JUNE 2021                             *
'**************************************************

MODE 1,16 '800x600 16 bits colour

DIM STRING TXT(15) LENGTH 1

#INCLUDE "./INC/FONTS.INC"

PAGE WRITE 1
LOAD JPG "./ASSETS/GamePad_Black.JPG"

INIT_FONT_K 'FONT 64X64 INIT

FRAMEBUFFER CREATE 800,600
FRAMEBUFFER WRITE
COLOUR RGB(BLACK),RGB(WHITE)

WII CLASSIC OPEN 3 'joystik on front panel

DO

BOX 10,15,780,158,,,RGB(BLUE)
PRINTK(-16,35,"WII CONTROLLER",4)
PRINTK(16,102,"   TESTING",4)

BOX 10,175,780,420,,,RGB(WHITE)
BLIT 0,0,191,200,417,330,1 ' Draw picture of joystik

'Testing bits of buttons
IF (CLASSIC(B) AND 1) <> 0 THEN CIRCLE 522,240,10,5,,,RGB(RED)     'R
IF (CLASSIC(B) AND 2) <> 0 THEN CIRCLE 428,347,10,5,,,RGB(RED)     'START
IF (CLASSIC(B) AND 4) <> 0 THEN CIRCLE 400,347,10,5,,,RGB(RED)     'HOME
IF (CLASSIC(B) AND 8) <> 0 THEN CIRCLE 369,347,10,5,,,RGB(RED)     'SELECT
IF (CLASSIC(B) AND 16) <> 0 THEN CIRCLE 276,240,10,5,,,RGB(RED)    'L
IF (CLASSIC(B) AND 32) <> 0 THEN CIRCLE 271,368,10,5,,,RGB(RED)    'DOWN
IF (CLASSIC(B) AND 64) <> 0 THEN CIRCLE 291,347,10,5,,,RGB(RED)    'RIGHT
IF (CLASSIC(B) AND 128) <> 0 THEN CIRCLE 271,327,10,5,,,RGB(RED)   'UP
IF (CLASSIC(B) AND 256) <> 0 THEN CIRCLE 252,347,10,5,,,RGB(RED)   'LEFT
IF (CLASSIC(B) AND 512) <> 0 THEN CIRCLE 522,218,10,5,,,RGB(RED)   'ZR
IF (CLASSIC(B) AND 1024) <> 0 THEN CIRCLE 526,318,10,5,,,RGB(RED)  'X
IF (CLASSIC(B) AND 2048) <> 0 THEN CIRCLE 565,347,10,5,,,RGB(RED)  'A
IF (CLASSIC(B) AND 4096) <> 0 THEN CIRCLE 487,347,10,5,,,RGB(RED)  'Y
IF (CLASSIC(B) AND 8192) <> 0 THEN CIRCLE 526,377,10,5,,,RGB(RED)  'B
IF (CLASSIC(B) AND 16384) <> 0 THEN CIRCLE 276,218,10,5,,,RGB(RED) 'ZL

'BLIT 0,350,250,520,300,78,1 'Clear space under box

BOX 267,460,129,129,,,RGB(CYAN)
BOX 402,460,129,129,,,RGB(CYAN)
LINE 466,460,466,588
LINE 402,525,530,525
LINE 332,460,332,588
LINE 267,525,395,525

CIRCLE (CLASSIC(LX)/2)+268,(589-CLASSIC(LY)/2),8,3,,,RGB(BLUE)
CIRCLE (CLASSIC(RX)/2)+402,(589-CLASSIC(RY)/2),8,3,,,RGB(BLUE)

PRINT @(75,500) "LEFT STICK"
PRINT @(75,514) "LX=";CLASSIC(LX)
PRINT @(75,528) "LY=";CLASSIC(LY)

PRINT @(650,500) "RIGHT STICK"
PRINT @(650,514) "RX=";CLASSIC(RX)
PRINT @(650,528) "RY=";CLASSIC(RY)

PRINT @(630,575) "PRESS 'ESC' TO EXIT"

FRAMEBUFFER WINDOW 0,0,0,B

IF KEYDOWN (1) = 27 THEN END
LOOP
